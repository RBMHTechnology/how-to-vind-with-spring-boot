plugins {
	id 'org.springframework.boot' version '2.1.9.RELEASE'
	id 'io.spring.dependency-management' version '1.0.6.RELEASE'
	id 'java'
}

group = 'at.redlink'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	// for dependency issues with restlet in solr
	maven {
		url 'http://maven.restlet.org'
	}
	// for snapshot vind testcontainer
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
}

dependencies {
	compile 'org.springframework.boot:spring-boot-starter-actuator'
	compile 'org.springframework.boot:spring-boot-starter-web'
	compile 'org.springframework.boot:spring-boot-configuration-processor'
	compile 'com.rbmhtechnology.vind:vind-api'

	// spring has a newer solr version, so force an older one
	compile("org.apache.solr:solr-core:7.5.0"){
		force = true
		exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
	}
	compile("org.apache.solr:solr-solrj:7.5.0"){
		force = true
	}

	runtime 'com.rbmhtechnology.vind:remote-solr-server'

	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'junit:junit'

	//vind testcontainers (the best way to test)
	testCompile 'io.redlink.utils:testcontainers:2.0.0-SNAPSHOT'

	// deal with dependecy hell if using solr embedded server
	testCompile 'com.rbmhtechnology.vind:vind-testsuite'
	testRuntime("io.dropwizard.metrics:metrics-core:3.2.6"){
		force = true
	}
}

dependencyManagement {
	dependencies {
		dependencySet(group: 'com.rbmhtechnology.vind', version: '2.3.0') {
			entry 'vind-api'
			entry 'embedded-solr-server'
			entry 'remote-solr-server'
			entry 'vind-testsuite'
		}
		dependency group: 'org.restlet.jee', name: 'org.restlet', version: '2.3.0'
		dependency group: 'junit', name: 'junit', version: '4.12'
	}
}